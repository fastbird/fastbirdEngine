/*
 -----------------------------------------------------------------------------
 This source file is part of fastbird engine
 For the latest info, see http://www.jungwan.net/
 
 Copyright (c) 2013-2015 Jungwan Byun
 
 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this software and associated documentation files (the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions:
 
 The above copyright notice and this permission notice shall be included in
 all copies or substantial portions of the Software.
 
 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 THE SOFTWARE.
 -----------------------------------------------------------------------------
*/

//----------------------------------------------------------------------------
// File: skySphere_equirectangular.hlsl
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
// Constant Buffer
//----------------------------------------------------------------------------
#include "EssentialEngineData/shaders/Constants.h"
#include "EssentialEngineData/shaders/CommonFunctions.h"

//----------------------------------------------------------------------------
// Textures
//----------------------------------------------------------------------------

Texture2D  gSkyTexture : register(t0);
//----------------------------------------------------------------------------
struct a2v
{
	float3 Position		: POSITION;
	float2 UV			: TEXCOORD;
};

//----------------------------------------------------------------------------
struct v2p
{
	float4 Position		: SV_Position;
	float2 UV			: TEXCOORD0;
	//float3 WorldDir		: TEXCOORD1;
};

//----------------------------------------------------------------------------
// VertexShader
//----------------------------------------------------------------------------
v2p skysphere_equirectangular_VertexShader(uint id : SV_VertexID)
{
	v2p Output;
	float4 outPos = float4((float)(id/2) * 4.0 - 1.0, (float)(id%2) * 4.0 - 1.0, 0.9999, 1.0);
	Output.Position = outPos;
	
	Output.UV.x = (float)(id/2) * 2.0;
	Output.UV.y = 1.0 - (float)(id%2) * 2.0;
	
	//float4 worldPos = mul(gInvViewProj, outPos);
	//worldPos.xyz /= worldPos.w;	
	//Output.WorldDir = worldPos.xyz - camPos;

	return Output;
}

float2 SphericalCoord2(float3 dir)
{	
	float u, v;
	if (dir.x == 0 && dir.y == 0)
		u = 0;
	else
		u = atan2(dir[1], dir[0]) / (2*PI) + 0.5;
	
	float len = length(dir.xy);
	if (dir.z == 0 && len == 0)
		v = 0;
	else
		v = atan2(dir[2], len) / PI + 0.5;	

	return float2(u, v);
}

//----------------------------------------------------------------------------
// PIXEL shader
//----------------------------------------------------------------------------
float4 skysphere_equirectangular_PixelShader(in v2p INPUT) : SV_Target
{
	float3 camPos = {gCamTransform[0][3], gCamTransform[1][3], gCamTransform[2][3]};
	float4 projectedPos = {INPUT.UV.x*2.f - 1.f, INPUT.UV.y * 2.f - 1.f, 0.99f, 1.0f};
	float4 worldPos = mul(gInvViewProj, projectedPos);	
	worldPos.xyz /= worldPos.w;
	float3 dir = normalize(worldPos - camPos);
	//return float4(dir.x * .5f + .5f, 0, 0, 1);
	return gSkyTexture.Sample(gLinearSampler, SphericalCoord2(dir));	
}